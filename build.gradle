// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.21' apply false
    id("org.jetbrains.kotlinx.kover") version "0.5.1"
    id 'com.adarshr.test-logger' version '3.2.0'
} 

allprojects {
    repositories {
        mavenCentral()
        google()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                def key
                if (rootProject.file("local.properties").exists()) {
                    def localProperties = new Properties()
                    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
                    key = localProperties.getProperty("mapbox.dl.token")
                }
                else {
                    key = "$System.env.MAPBOX_KEY"
                }
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = 'mapbox'
                // Use the secret token you stored in gradle.properties as the password
                password = key
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.withType(Test) {
    testLogging {
        events "standardOut", "passed", "skipped", "failed"
    }
}
